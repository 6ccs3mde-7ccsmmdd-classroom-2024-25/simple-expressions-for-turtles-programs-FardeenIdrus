grammar uk.ac.kcl.inf.mdd2.Turtles with org.eclipse.xtext.common.Terminals

generate turtles "http://www.ac.uk/kcl/inf/mdd2/Turtles"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

TurtleProgram:
statements += Statement*
;

Statement:
MoveStatement |
TurnStatement |
LoopStatement |
VariableDeclaration
;

VariableDeclaration:
"var" name=ID "=" value=Expression
;

LoopStatement:
count = Expression "times" "do"
statements += Statement+
"end"
;

MoveStatement:
command = MoveCommand "(" steps = Expression ")"
;

enum MoveCommand:
forward | backward
;

TurnStatement:
"turn" command = TurnCommand "by" degrees = Expression "degrees"
;

enum TurnCommand:
left | right
;

Expression:
PlusExpression
;

PlusExpression returns Expression:
PrimaryExpression ({PlusExpression.left=current} "+" right=PrimaryExpression)*
;

PrimaryExpression returns Expression:
IntConstant |
RealConstant |
VariableReference |
"(" Expression ")"
;

IntConstant:
value=INT
;

RealConstant:
value=REAL
;

VariableReference:
variable=[VariableDeclaration]
;

REAL returns ecore::EFloat hidden():
INT? "." INT
;